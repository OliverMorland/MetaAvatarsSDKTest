// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel CSMain OVR_HAS_TANGENTS
#pragma kernel CSMain OVR_DOUBLE_BUFFER_OUTPUT
#pragma kernel CSMain OVR_HAS_TANGENTS OVR_DOUBLE_BUFFER_OUTPUT

#include "OvrVertexCompute.cginc"
#include "OvrOutputBufferCompute.cginc"
#include "OvrMorphsCompute.cginc"
#include "OvrSkinningCompute.cginc"


////////////////////////////////////////////////////////////////////////////
// Data Layout(s)
////////////////////////////////////////////////////////////////////
///
/// There are 4 buffers
///
/// "StaticDataBuffer" - All the 'static data' for any number of "mesh entries".
/// There can be multiple "mesh entries" in the buffer to facilitate batching setups.
///
/// {
///   [static_mesh_meta_data] -> Array of "headers" which holds byte offsets and other metadata about the static mesh data
///   [neutral_positions] -> An array of "neutral pose positions"
///   [neutral_normals] -> An array of "neutral pose normals"
///   [neutral_tangents] -> An array (might be if no tangents) of "neutral pose tangents"
///   [joint_weights] -> An array of joint weights
///   [joint_indices] -> An array of joint indices
///   [output_indices] -> An array of output indices per vertex. Serves as a mapping from input vertex order to output vertex order
///   [morph_target_deltas] -> Arrays of morph target related information
/// }
///
///  "Dynamic Buffer" - Stores all of the "dynamic" (can change frequently) data for a mesh.
///  {
///     [vertex_instance_data] -> An array of all the "vertex instance data" potentially across multiple "mesh instances"
///     [mesh_instance_meta_data] -> An array of "mesh instance meta data" for the different "mesh instances"
///     [joint_matrices] -> An array joint matrices for all mesh instances
///     [joint_weights] -> An array of joint weights for all mesh instances
///  }
///
///  "Position output buffer" - Stores only the positions of the final output.
///  // Might be interleaved "double buffered" output for same vertex next to each other
///  [positions] -> An array of output positions
///
///  "Frenet output buffer" - Stores interleaved normal + tangents (if applicable) information
///  // Might be interleaved with "double buffered" output
///  [normal, tangents] -> An array of interleaved normals and tangents (if applicable)


////////////////////////////////
// Data Structures
////////////////////////////////
struct StaticMeshMetaData {
  // Static data offsets

  // Offset to an array of positions(x), normals(y), tangents(z), joint weights (w)
  uint4 attributeAndJointWeightsOffsetBytes;

  // Morph target deltas offset bytes(x), numMorphs(y), numMorphedVertices(z), joint indices (w)
  uint4 morphTargetInfoAndJointIndicesOffsetBytes;

  // Offset to an array of output indices (x) (yzw) - unused
  uint4 outputIndexOffset;

  float4 vertexInputPositionOffset; // Float4s for alignment, w unused (is this needed?)
  float4 vertexInputPositionScale;
  float4 vertexOutputPositionOffset; // Float4s for alignment, w unused (is this needed?)
  float4 vertexOutputPositionScale;

  float4 morphTargetsPosRange;
  float4 morphTargetsNormRange;
  float4 morphTargetsTanRange;

  // TODO* Padding/Alignment?
};

struct MeshInstanceMetaData {
  // meshStaticDataOffsetBytes(x) morphTargetWeightsOffsetBytes(y) jointMatricesOffsetBytes(z) writeToSecondSliceAddress (w)
  uint4 properties;

  // outputPositionBufferOffsetBytes(x) outputBufferOffsetBytes(y)
  uint4 outputOffsets;

  // TODO* Padding/Alignment?
};

struct VertexInstanceData {
  // meshInstanceDataOffsetBytes(x) vertexIndexInMesh(y)
  uint4 properties;

  // TODO* Padding/Alignment?
};

////////////////////////////////////////////
/// Uniforms and Buffers
////////////////////////////////////////////

// TODO*: Layout diagram
ByteAddressBuffer _StaticDataBuffer; // "Bag of Bytes" (really bag of dwords)
ByteAddressBuffer _DynamicDataBuffer; // "Bag of Bytes" (really bag of dwords)
RWByteAddressBuffer _PositionOutputBuffer; // "Bag of Bytes" (really bag of dwords)
RWByteAddressBuffer _FrenetOutputBuffer; // "Bag of Bytes" (really bag of dwords)

int _DispatchStartVertIndex;
int _DispatchEndVertIndex;

// Using these below instead of #ifdefs to decrease variants (at the cost of
// a "static" branch)
bool _EnableMorphTargets; // 0 = false, 1 = true
bool _EnableSkinning; // 0 = false, 1 = true
int _MaxJointsToSkin;
int _VertexPositionsDataFormat;
int _MorphTargetDeltasDataFormat;
int _PositionOutputBufferDataFormat;

////////////////////////////////////////////////////////
// Helper Functions
////////////////////////////////////////////////////////

uint GetMorphTargetDeltasOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  // Morph target deltas offset bytes(x)
  return static_mesh_meta_data.morphTargetInfoAndJointIndicesOffsetBytes.x;
}

uint GetNumMorphs(StaticMeshMetaData static_mesh_meta_data) {
  // numMorphs(y)
  return static_mesh_meta_data.morphTargetInfoAndJointIndicesOffsetBytes.y;
}

uint GetNumMorphedVertices(StaticMeshMetaData static_mesh_meta_data) {
  // numMorphedVertices(z)
  return static_mesh_meta_data.morphTargetInfoAndJointIndicesOffsetBytes.z;
}

uint GetJointIndicesOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.morphTargetInfoAndJointIndicesOffsetBytes.w;
}

uint GetOutputPositionBufferOffsetBytes(MeshInstanceMetaData mesh_instance_meta_data) {
  // outputPositionBufferOffsetBytes(x)
  return mesh_instance_meta_data.outputOffsets.x;
}

uint GetOutputBufferOffsetBytes(MeshInstanceMetaData mesh_instance_meta_data) {
  // outputPositionBufferOffsetBytes(x)
  return mesh_instance_meta_data.outputOffsets.y;
}

uint GetMeshStaticDataOffsetBytes(MeshInstanceMetaData mesh_instance_meta_data) {
  return mesh_instance_meta_data.properties.x;
}

uint GetMorphTargetWeightsOffetBytes(MeshInstanceMetaData mesh_instance_meta_data) {
  return mesh_instance_meta_data.properties.y;
}

uint GetJointMatricesOffsetBytes(MeshInstanceMetaData mesh_instance_meta_data) {
  // jointMatricesOffsetBytes(w)
  return mesh_instance_meta_data.properties.z;
}

bool WriteToSecondSlice(MeshInstanceMetaData mesh_instance_meta_data, ByteAddressBuffer data_buffer) {
  // Grab address to read from from the mesh instance meta data
  const int address = mesh_instance_meta_data.properties.w;

  return (bool)data_buffer.Load(address);
}

uint GetMeshInstanceDataOffsetBytes(VertexInstanceData vertex_instance_data) {
  return vertex_instance_data.properties.x;
}

uint GetVertexIndexWithinMesh(VertexInstanceData vertex_instance_data) {
  return vertex_instance_data.properties.y;
}

uint GetInputPositionOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.attributeAndJointWeightsOffsetBytes.x;
}

uint GetInputNormalOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.attributeAndJointWeightsOffsetBytes.y;
}

uint GetInputTangentOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.attributeAndJointWeightsOffsetBytes.z;
}

uint GetJointWeightsOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.attributeAndJointWeightsOffsetBytes.w;
}

uint GetOutputIndexOffsetBytes(StaticMeshMetaData static_mesh_meta_data) {
  return static_mesh_meta_data.outputIndexOffset.x;
}

StaticMeshMetaData GetStaticMeshMetaData(ByteAddressBuffer data_buffer, MeshInstanceMetaData mesh_instance) {
  StaticMeshMetaData meta_data;

  // meshStaticDataHeaderOffsetBytes(y)
  const int address = GetMeshStaticDataOffsetBytes(mesh_instance);

  // NOTE!!!!!!
  // Order here is very important and must match struct declaration
  // Until Shader Model 6.2 is supported by Unity, this will remain in all its fragile glory

  // 4 byte stride per uint
  // 16 byte stride per float4/uint4
  meta_data.attributeAndJointWeightsOffsetBytes = data_buffer.Load4(address);
  meta_data.morphTargetInfoAndJointIndicesOffsetBytes = data_buffer.Load4(address + 16);
  meta_data.outputIndexOffset = data_buffer.Load4(address + 32);
  meta_data.vertexInputPositionOffset = UnpackFloat4x32(data_buffer, address + 48);
  meta_data.vertexInputPositionScale = UnpackFloat4x32(data_buffer, address + 64);
  meta_data.vertexOutputPositionOffset = UnpackFloat4x32(data_buffer, address + 80);
  meta_data.vertexOutputPositionScale = UnpackFloat4x32(data_buffer, address + 96);
  meta_data.morphTargetsPosRange = UnpackFloat4x32(data_buffer, address + 112);
  meta_data.morphTargetsNormRange = UnpackFloat4x32(data_buffer, address + 128);
  meta_data.morphTargetsTanRange = UnpackFloat4x32(data_buffer, address + 144);

  return meta_data;
}

MeshInstanceMetaData GetMeshInstanceMetaData(ByteAddressBuffer data_buffer, VertexInstanceData vert_instance) {
  MeshInstanceMetaData mesh_instance_data;

  const int address = GetMeshInstanceDataOffsetBytes(vert_instance);

  // NOTE!!!!!!
  // Order here is very important
  // Until Shader Model 6.2 is supported by Unity, this will remain in all its fragile glory
  mesh_instance_data.properties = data_buffer.Load4(address);
  mesh_instance_data.outputOffsets = data_buffer.Load4(address + 16);

  return mesh_instance_data;
}

VertexInstanceData GetVertexInstanceData(ByteAddressBuffer data_buffer, int address) {
  VertexInstanceData vertex_instance_data;

  // NOTE!!!!!!
  // Order here is very important
  // Until Shader Model 6.2 is supported by Unity, this will remain in all its fragile glory
  vertex_instance_data.properties = data_buffer.Load4(address);

  return vertex_instance_data;
}

Vertex GetVertexStaticData(
  StaticMeshMetaData static_mesh_meta_data,
  uint vertex_index)
{
  return GetVertexStaticData(
    _StaticDataBuffer,
    GetInputPositionOffsetBytes(static_mesh_meta_data),
    GetInputNormalOffsetBytes(static_mesh_meta_data),
    GetJointWeightsOffsetBytes(static_mesh_meta_data),
    OVR_FORMAT_FLOAT_32,
    GetJointIndicesOffsetBytes(static_mesh_meta_data),
    OVR_FORMAT_UINT_16,
    GetOutputIndexOffsetBytes(static_mesh_meta_data),
    _VertexPositionsDataFormat,
    static_mesh_meta_data.vertexInputPositionOffset,
    static_mesh_meta_data.vertexInputPositionScale,
    vertex_index);
}

VertexInstanceData GetVertexInstanceData(uint vert_instances_index) {
  // ASSUMPTION: The vertex instance data is the first data in the dynamic buffer
  static const uint STRIDE = 4 * 4; // 4 32-bit uints
  const int address = vert_instances_index * STRIDE;

  VertexInstanceData vertex_instance_data;
  vertex_instance_data.properties = _DynamicDataBuffer.Load(address);

  return vertex_instance_data;
}

////////////////////////////////////////
// Main
////////////////////////////////////////

[numthreads(64,1,1)]
void CSMain(uint3 id : SV_DispatchThreadID) {
  const uint vert_instances_index = (uint)_DispatchStartVertIndex + id.x;

  // Benchmark if this or just having some "empty" data is faster (though more memory)
  if (vert_instances_index >= (uint)_DispatchEndVertIndex) {
    return;
  }

  // ASSUMPTION: The vertex instance data is the first data in the dynamic buffer
  const VertexInstanceData vertex_instance = GetVertexInstanceData(vert_instances_index);

  // From that data, grab the mesh instance data and mesh static data
  const MeshInstanceMetaData mesh_instance_meta_data = GetMeshInstanceMetaData(
    _DynamicDataBuffer,
    vertex_instance);

  const StaticMeshMetaData static_mesh_meta_data = GetStaticMeshMetaData(
    _StaticDataBuffer,
    mesh_instance_meta_data);

  // Get the vertex id "within the mesh"
  const uint vertex_index = GetVertexIndexWithinMesh(vertex_instance);

  // Grab "vertex static data" out of the static data
  Vertex vertex = GetVertexStaticData(static_mesh_meta_data, vertex_index);

  // Compiler should hopefully optimize out any potential branches due to static const bool values,
  // and otherwise, branches should be based on uniform parameters passed in which
  // should make their just the branch and not cause diverging branches across workgroups
  // Compiler should also optimize out unused parameters
#if defined(OVR_HAS_TANGENTS)
  static const bool has_tangents = true;
#else
  static const bool has_tangents = false;
#endif

#if defined(OVR_DOUBLE_BUFFER_OUTPUT)
  static const bool double_buffer = true;
  const bool write_to_second_slice = WriteToSecondSlice(mesh_instance_meta_data, _DynamicDataBuffer);
#else
  static const bool double_buffer = false;
  static const bool write_to_second_slice = false;
#endif


  float4 vertex_tangent = float4(0.0, 0.0, 1.0, 1.0);
  if (has_tangents) {
    uint tangents_start_address = GetInputTangentOffsetBytes(static_mesh_meta_data);
    vertex_tangent = GetNeutralPoseTangent(_StaticDataBuffer, tangents_start_address, vertex_index);
  }

  // Apply morphs
  [branch]
  if (_EnableMorphTargets) {
    ApplyRectangularMorphs(
      _StaticDataBuffer,
      _DynamicDataBuffer,
      GetMorphTargetDeltasOffsetBytes(static_mesh_meta_data),
      GetMorphTargetWeightsOffetBytes(mesh_instance_meta_data),
      GetNumMorphs(static_mesh_meta_data),
      GetNumMorphedVertices(static_mesh_meta_data),
      _MorphTargetDeltasDataFormat,
      vertex.position,
      static_mesh_meta_data.morphTargetsPosRange.xyz,
      vertex.normal,
      static_mesh_meta_data.morphTargetsNormRange.xyz,
      vertex_tangent,
      static_mesh_meta_data.morphTargetsTanRange.xyz,
      vertex.vertexIndex,
      has_tangents);
  }

  // Apply skinning
  [branch]
  if (_EnableSkinning) {
    ApplySkinning(
      _MaxJointsToSkin,
      _DynamicDataBuffer,
      GetJointMatricesOffsetBytes(mesh_instance_meta_data),
      vertex.position,
      vertex.normal,
      vertex_tangent.xyz,
      vertex.jointWeights,
      vertex.jointIndices,
      has_tangents);
  }

  StoreVertexOutput(
    _PositionOutputBuffer,
    _FrenetOutputBuffer,
    GetOutputPositionBufferOffsetBytes(mesh_instance_meta_data),
    GetOutputBufferOffsetBytes(mesh_instance_meta_data),
    static_mesh_meta_data.vertexOutputPositionOffset.xyz,
    static_mesh_meta_data.vertexOutputPositionScale.xyz,
    _PositionOutputBufferDataFormat,
    vertex.position,
    vertex.normal,
    vertex_tangent,
    has_tangents,
    double_buffer,
    write_to_second_slice,
    vertex.vertexIndex);
}
